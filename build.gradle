/*
 * Copyright (c) 2016 EMC Corporation. All Rights Reserved.
 *
 * Licensed under the EMC Software License Agreement for Free Software (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 * https://github.com/EMCECS/ecs-alfresco-content-store/blob/master/LICENSE.txt
 */
description = 'Alfresco ECS Content Source - lets an Alfresco Repository instance use ECS as a back-end content store. http://docs.alfresco.com/5.1/references/dev-extension-points-custom-content-store.html'
group = 'com.emc.ecs'
version = '1.0.0-rc2'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public/'
        }
    }
    dependencies {
        classpath group: 'org.alfresco.gradle', name: 'alfresco-gradle-plugins', version: '0.1.9.1'
        classpath group: 'com.yahoo.platform.yui', name: 'yuicompressor', version: '2.4.6'
    }
    configurations.classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'amp'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://artifacts.alfresco.com/nexus/content/groups/public/'
    }
}

configurations {
    jars
    deps
    ampLib {
        extendsFrom deps
        transitive = true
    }
    ampLib.exclude group: 'org.slf4j'
    ampLib.exclude group: 'org.apache.httpcomponents'
    ampLib.exclude group: 'commons-codec'
    ampLib.exclude module: 'commons-lang3'
    compile {
        extendsFrom deps
    }
}


dependencies {
    deps "com.emc.ecs:object-client:2.2.4",
         "org.apache.commons:commons-lang3:3.4"
}

defaultTasks 'distZip'

distZip.dependsOn amp

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    jars jar
    jars sourcesJar
}

jar {
    doFirst {
        manifest {
            attributes 'Implementation-Version': project.version,
                    'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        }
    }
}

distributions {
    main {
        contents {
            from configurations.jars.artifacts.files
            from('.') {
                include '*.txt'
            }
            from('build/distributions') {
                include '*.amp'
            }
            into('3rd-party-licenses') {
                from '3rd-party-licenses'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
